ACTOR PostalDudeSr : PlayerPawn
{

	Player.DisplayName "The Postal Dude, Sr."
	Player.SoundClass "Dude"
    Mass 100
    player.viewheight 44
    player.attackzoffset 16
    GibHealth 20
    PainChance 255
    Species "Players"
    Player.ColorRange 112, 127
	Player.StartItem "StarterSMG"
	Player.StartItem "Fist"
	Player.StartItem "Clip", 100
    Player.JumpZ 7.4
	XScale 0.58
	YScale 0.58
	Player.WeaponSlot 2, "StarterSMG"
	
    +SOLID
    +THRUSPECIES
    +MTHRUSPECIES
    +DONTHURTSPECIES
    +THRUGHOST

   States
    {
    Spawn:
        TNT1 A 0 NoDelay SetPlayerProperty(0, 1, PROP_FROZEN)
        TNT1 A 0 A_Changeflag("NOPAIN", 1)
        DUDE A 1
        TNT1 A 0
        {
            SetPlayerProperty(0, 0, PROP_FROZEN);
            A_Changeflag("NOPAIN", 0);
            if (GameType() == 2) //Deathmatch
            {
                A_ChangeFlag("THRUSPECIES", 0);
                A_ChangeFlag("MTHRUSPECIES", 0);
                A_ChangeFlag("DONTHURTSPECIES", 0);
            }
        }
    StandStill:
        DUDE A 1 A_JumpIf(velz != 0, "IsJumpin")
        TNT1 A 0
        loop
    
    See:
        DUDE AAAA 6 A_JumpIf(velz != 0, "IsJumpin")
        TNT1 A 0
        goto StandStill

    IsJumpin:
        DUDE A 3
        TNT1 A 0 A_JumpIf(velz != 0, "InAir")
        goto StandStill
        
    InAir:
        DUDE A 1
        TNT1 A 0 A_JumpIf(velz == 0, "Land")
        loop

    Land:
        TNT1 A 0 A_SetPitch(3.0 + pitch)
        DUDE AAA 1 A_SetPitch(-1.0 + pitch)
        goto StandStill
    Pain:
        TNT1 A 0 A_Pain
        DUDE A 5
        goto See
    Death:
        DUDE A 10
        DUDE A 10 A_PlayerScream
        DUDE A 10 A_NoBlocking
        DUDE AAA 10
        DUDE A -1
        Stop
	}
}